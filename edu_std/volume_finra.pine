//@version=5
indicator("FINRA Daily Short Sale Volume", "FINRA Shorts", format = format.volume)

bool overwriteSymbolInput = input.bool(false, "Other symbol", inline = "OS", group = "Short Volume")
string tickerInput = input.symbol("", "", inline = "OS", group = "Short Volume")
int maLengthInput = input.int(20, "Length", group = "Moving Average", minval = 2, maxval = 4999)

string symbolOnly = str.substring(tickerInput, str.pos(tickerInput, ":") + 1)
string userSymbol = overwriteSymbolInput ? symbolOnly : syminfo.ticker
string shortVolumeTicker = str.format("FINRA:{0}_SHORT_VOLUME", userSymbol)

[shortVolume, shortVolumeMa] = request.security(shortVolumeTicker, "D", [close, ta.sma(close, maLengthInput)], ignore_invalid_symbol = true)

var string[] validPrefixes = array.from("BATS", "NASDAQ", "NYSE", "AMEX")
if not array.includes(validPrefixes, syminfo.prefix) and not overwriteSymbolInput
    runtime.error(str.format("Short Sale Volume data is not available for the `{0}` exchange. Supported exchanges are: NASDAQ, NYSE, NYSE ARCA.", syminfo.prefix))

if barstate.islastconfirmedhistory and na(shortVolume)
    runtime.error(str.format("No Short Volume data found for the `{0}` symbol.", tickerInput))

if barstate.isfirst
    var table symbolTable = table.new(position.bottom_left, 1, 1, bgcolor = color.new(color.gray, 0))
    table.cell(symbolTable, 0, 0, text = userSymbol + " short sale volume data: " + shortVolumeTicker, text_color = color.white, text_size = size.small)

color shortVolumeColor = shortVolume > shortVolumeMa ? color.new(color.red, 20) : color.new(color.red, 60)
plot(shortVolume, "Daily Short Sale Volume", shortVolumeColor, 1, plot.style_columns)
plot(shortVolumeMa, "Volume Moving Average", color.gray)
